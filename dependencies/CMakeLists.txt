cmake_minimum_required(VERSION 3.7.0 FATAL_ERROR)

option (GA_SUPERBUILD "This will build blas+lapack if required" OFF)

if(DEFINED TAMM_PROC_COUNT)
    set(TAMM_PROC_COUNT ${TAMM_PROC_COUNT})
else()
    include(ProcessorCount)
    ProcessorCount(TAMM_PROC_COUNT)
    message(STATUS "Number of cores detected = ${TAMM_PROC_COUNT}")
endif()

message(STATUS "Using ${TAMM_PROC_COUNT} cores for building TAMM dependencies.")
message("Please set the TAMM_PROC_COUNT cmake variable to change the cpu count.")

if(GA_SUPERBUILD)
    project (GA_DEPENDENCIES C CXX Fortran)
    set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_CURRENT_SOURCE_DIR}/cmake/")

    if (CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
        set (CMAKE_INSTALL_PREFIX "${PROJECT_BINARY_DIR}/tamm-deps" CACHE PATH "default install path" FORCE)
    endif()

    if (NOT BLAS_LIBRARIES)
        include(blas_lapack)
    else()
        add_custom_target(BLAS_LAPACK ALL)
    endif()

    include(ExternalProject)
    ExternalProject_Add (TAMM_DEPS
            DEPENDS BLAS_LAPACK
            SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}
            BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}
            CMAKE_ARGS -DGA_SUPERBUILD=OFF
            -DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}
            -DCMAKE_Fortran_COMPILER=${CMAKE_Fortran_COMPILER} -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
            -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER} -DMPI_INCLUDE_PATH=${MPI_INCLUDE_PATH}
            -DMPI_LIBRARY_PATH=${MPI_LIBRARY_PATH} -DMPI_LIBRARIES=${MPI_LIBRARIES}
            -DBLAS_LIBRARIES=${BLAS_LIBRARIES} -DLAPACK_LIBRARIES=${LAPACK_LIBRARIES}
            -DSCALAPACK_LIBRARIES=${SCALAPACK_LIBRARIES} -DTAMM_PROC_COUNT=${TAMM_PROC_COUNT}
            -DBLAS_INCLUDE_PATH=${BLAS_INCLUDE_PATH} -DBLAS_LIBRARY_PATH=${BLAS_LIBRARY_PATH}
            INSTALL_COMMAND cmake -E echo "Finished installing all TAMM dependencies!"
            )
    return()

else()

    project (TAMM_DEPENDENCIES C CXX)

    if (CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
        set (CMAKE_INSTALL_PREFIX "${PROJECT_BINARY_DIR}/tamm-deps" CACHE PATH "default install path" FORCE)
    endif()

    include(ExternalProject)

    set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${PROJECT_SOURCE_DIR}/cmake/")

    include(libint)
    include(eigen)
    #include(globalarrays)
    #include(antlr)

    set(TAMM_TOOLCHAIN_FILE "${PROJECT_BINARY_DIR}/tamm_build.cmake")
    file(WRITE ${TAMM_TOOLCHAIN_FILE} "#This file is automatically generated. Please do not edit it. \n")

    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(CMAKE_BUILD_TYPE ${CMAKE_BUILD_TYPE}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(TAMM_PROC_COUNT ${TAMM_PROC_COUNT}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(CMAKE_C_COMPILER ${CMAKE_C_COMPILER}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(CMAKE_CXX_COMPILER ${CMAKE_CXX_COMPILER}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(CMAKE_Fortran_COMPILER ${CMAKE_Fortran_COMPILER}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(TAMM_DEPENDENCIES_INSTALL_PREFIX ${CMAKE_INSTALL_PREFIX}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(CMAKE_EXPORT_COMPILE_COMMANDS ${CMAKE_EXPORT_COMPILE_COMMANDS}) \n\n")

    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(MPI_INCLUDE_PATH ${MPI_INCLUDE_PATH}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(MPI_LIBRARY_PATH ${MPI_LIBRARY_PATH}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(MPI_LIBRARIES ${MPI_LIBRARIES}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(BLAS_INCLUDE_PATH ${BLAS_INCLUDE_PATH}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(BLAS_LIBRARY_PATH ${BLAS_LIBRARY_PATH}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(BLAS_LIBRARIES ${BLAS_LIBRARIES}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(LAPACK_LIBRARIES ${LAPACK_LIBRARIES}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(SCALAPACK_LIBRARIES ${SCALAPACK_LIBRARIES}) \n\n")

    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(LIBINT_INSTALL_PATH ${LIBINT_INSTALL_PATH}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(ANTLR_CPPRUNTIME_PATH ${ANTLR_CPPRUNTIME_PATH}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(GA_INSTALL_PATH ${GA_INSTALL_PATH}) \n")
    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(EIGEN3_INSTALL_PATH ${EIGEN3_INSTALL_PATH}) \n\n")

    file(APPEND ${TAMM_TOOLCHAIN_FILE} "set(TALSH_INSTALL_PATH ${TALSH_INSTALL_PATH}) \n")


endif()
